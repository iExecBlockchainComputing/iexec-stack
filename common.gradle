
// define all path and variables used in gradle
project.ext{
    PROJECT_TAG = project.name + '-' + version
    root = '.'
    build = root + '/build'
    libXtremWeb = root + '/libXtremWeb'
    src = root + '/src'
    res = src + '/main/resources'
    conf = res + '/conf'
    benchmark = res + '/benchmark'
    templates = res + '/templates'
    doc = res + '/doc'
    docker = res + '/docker'
    installers = res + '/installers'
    misc = res + '/misc'
    scripts = res + '/scripts'
    linuxInstallers = installers + '/linux'
    macInstallers = installers + '/macosx'
    opt = '/opt'
    dist = build + '/dist/' + PROJECT_TAG
    distBin = dist + '/bin'
    distLib = dist + '/lib'
    distDoc = dist + '/doc'
    distMacInstallers = dist + "/installers/macosx"
    privateEtc = '/private/etc'
}

def envuid=UUID.randomUUID().toString()
def adminuid=UUID.randomUUID().toString()
def workeruid=UUID.randomUUID().toString()
def vworkeruid=UUID.randomUUID().toString()
def admingroupuid=UUID.randomUUID().toString()
def publicworkersgroup=UUID.randomUUID().toString()
def gitShortCommit = 'git rev-parse --short HEAD'.execute().text.trim()
def isSnapshotVersion = project.version.contains("SNAPSHOT")

project.ext.getDockerImageVersion = {

    if(isSnapshotVersion){
        return project.version + "-" + gitShortCommit
    } else {
        return project.version
    }
}

// Define custom properties that can be used everywhere
project.ext.getCustomProperties = {

    def props = new Properties()
    // add all properties from conf file
    file(conf + "/xwconfigure.values").withInputStream { props.load(it) }
    for (String key : props.stringPropertyNames()) {
        props.setProperty(key, props.getProperty(key).replace('\'', ''))
    }

    // add other custom properties
    props.setProperty("ENVUID", envuid)
    props.setProperty("ADMINUID", adminuid)
    props.setProperty("WORKERUID", workeruid)
    props.setProperty("VWORKERUID", vworkeruid)
    props.setProperty("ADMINGROUPUID", admingroupuid)
    props.setProperty("PUBLICWORKERSGROUPUID", publicworkersgroup)
    props.setProperty("XWVERSION", version)
    props.setProperty("DOCKER_IMAGE_VERSION", getDockerImageVersion())
    props.setProperty("INSTALLDIR", opt)


    def path = 'pwd'.execute().text.trim()
    def keystore = path + "/build/dist/" + PROJECT_TAG + "/keystore/"
    def docker = path + "/build/dist/" + PROJECT_TAG + "/docker/"

    props.setProperty("CERTIFICATE", docker + "xwscheduler.pem")
    props.setProperty("SSLTRUSTSTORE", keystore + "cacerts")

    def certname = "cn=" + props.CERTCN + ",o=" + props.CERTO + ",c=" + props.CERTC
    if (props.CERTOU != "") {
        certname += ",ou=" + props.CERTOU
    }
    if (props.CERTL != "") {
        certname += ",l=" + props.CERTL
    }
    props.setProperty("CERTNAME", certname)

    return props
}
